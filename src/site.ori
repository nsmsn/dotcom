{
  blank.html = "This page intentionally left blank."

  assets

  img: {
    ...img
    maps: {
      notes: cache(maps.ori(data/notes.ori), files:cache/notes)
      posts: cache(maps.ori(data/posts.ori), files:cache/posts)
    }
  }

  library.html = templates/library.ori(data/library.json)

  posts = {
    ...map(data/posts.ori, templates/post.ori)
    index.html = templates/posts-index.ori(data/posts.ori)
    feed.json = json(feed-posts.ori(data/posts.ori))
    feed.xml = rss(feed-posts.ori(data/posts.ori))
  }

  notes = {
    ...map(data/notes.ori, templates/note.ori)
    (noteSets) = paginate(data/notes.ori, 10)
    pages/ = map(noteSets, {
      extension: "->.html"
      value: templates/notes-index.ori
    })
    index.html = pages/1.html
    feed.json = json(feed-notes.ori(data/notes.ori))
    feed.xml = rss(feed-notes.ori(data/notes.ori))
  }

  tags = {
    ...map(data/tags.ori, {
      key: (items, tag) => `${ slug(tag) }.html`
      value: templates/tag.ori
    })
    index.html = templates/tags-index.ori(data/tags.ori)
  }

  vinyl.html = templates/vinyl.ori(data/vinyl.json)

  work/ = map(map(./work, mdHtml), templates/page.ori)

  index.html = templates/index.ori(
    first(data/posts.ori)
    take(data/notes.ori, 3)
  )

  ...map(map(slash, mdHtml), templates/page.ori)

  feed.json = json(feed.ori(data/combined.ori))
  feed.xml = rss(feed.ori(data/combined.ori))

  _redirects
  favicon.ico
  humans.txt
  robots.txt 

  search.html = templates/search.ori()
}

// Only build each page once
→ (site) => cache(site)

// Use the pages to make both the site and the search index
→ (cached) => {
    ...cached
    pagefind/ = once(=package:@weborigami/pagefind(cached))
  }
